#!/usr/bin/env fish

if test -z $ANTHROPIC_API_KEY
    echo "You need to set the ANTHROPIC_API_KEY environment variable"
    exit 1
end

# Get target branch from config or fallback to main
set target_branch (git config --get zemzale.release)
if test $status -ne 0
    set target_branch "main" 
end

# Get current branch for source
set source_branch (git branch --show-current)
if test $status -ne 0
    echo "Failed to get current branch"
    exit 1
end

set PROMPT "
# Rules

- Be extremely concise, sacrifice grammar for the sake of concision
- Use a professional tone, If no JIRA ID is found, use just skip it.

# Task

Create an MR title and description based on the git information below. 

# Format:
[JIRA-ID] Brief action-oriented title (under 70 chars), no periods

## Why
Explain the purpose and business value of these changes.

## What
Summarize the technical implementation details.

## How to Test
Include steps to verify the changes work correctly.

## Notes
Add any additional context, dependencies, or known issues.
"

# Generate both title and description in one prompt
git log -p $target_branch..$source_branch | mods -R "mr" -f $PROMPT  > mr_content.md
if test $status -ne 0
    echo "Failed to generate content"
    exit 1
end 

nvim mr_content.md

# Extract title and description
set TITLE (head -n 1 mr_content.md)
if test $status -ne 0
    echo "Failed to extract title"
    exit 1
end

set DESCRIPTION (tail -n +3 mr_content.md | string collect)
if test $status -ne 0
    echo "Failed to extract description"
    exit 1
end

rm mr_content.md

# Create the MR
glab mr create -a=@me --remove-source-branch --push -t "$TITLE" -d "$DESCRIPTION" -s $source_branch -b $target_branch

